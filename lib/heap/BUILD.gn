# Copyright 2016 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//gnbuild/config.gni")

config("_heap_config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
}

module("heap") {
  configs += [
    "//kernel/kernel:enable_vm",
    "//kernel/lib/console:enable_console",
  ]
  public_configs = [ ":_heap_config" ]
  public = [
    "include/lib/heap.h",
  ]
  include_dirs = [
    "//kernel/lib/io/include",
    "//kernel/lib/libc/include",
  ]
  sources = [
    "heap_wrapper.c",
    "new.cpp",
    "page_alloc.c",
  ]

  # TODO(phosek): this dependency, even though correct as heap
  # implementation does depend on console if enabled currently
  # causes a cyclic dependency
  # deps = [
  #   "//kernel/lib/console",
  # ]
  if (heap_implementation == "cmpctmalloc") {
    deps = [
      "//kernel/lib/heap/cmpctmalloc",
    ]
  } else if (heap_implementation == "miniheap") {
    deps = [
      "//kernel/lib/heap/miniheap",
    ]
  } else {
    assert(false, "Unsupported heap implementation")
  }
}
