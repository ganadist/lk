# Copyright 2016 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//gnbuild/config.gni")

declare_args() {
  # Userboot filesystem image filename.
  user_bootfs = "$root_gen_dir/user.bootfs"
}

# Whether to embed the bootfs into the kernel image.
embed_user_bootfs = enable_magenta && enable_userboot

config("enable_userboot") {
  if (enable_userboot) {
    defines = [ "WITH_LIB_USERBOOT=1" ]
  } else {
    defines = [ "WITH_LIB_USERBOOT=0" ]
  }
}

# TODO(phosek): this should be enabled only if magenta is
module("userboot") {
  public_configs = [ ":enable_userboot" ]
  sources = [
    "userboot.cpp",
  ]
  deps = [
    "//kernel/lib/console",
    "//kernel/lib/elf",
    "//kernel/lib/libc",
    "//kernel/lib/magenta",
    "//kernel/lib/utils",
  ]
  if (enable_shell) {
    deps += [ "//kernel/app/shell" ]
  }
  if (embed_user_bootfs) {
    bootfs_file = rebase_path(user_bootfs, root_build_dir)
    defines = [
      "EMBED_USER_BOOTFS=1",
      "USER_BOOTFS_FILENAME=\"${bootfs_file}\"",
    ]
    sources += [ "bootfs.S" ]
    inputs = [ user_bootfs ]
    deps += [ "//kernel/project/virtual:bootfs" ]
  }
}
